(ns immersa.ui.editor.views
  (:require
    ["@clerk/clerk-react" :refer [useUser useAuth]]
    ["firebase/auth" :refer [getAuth signInWithCustomToken]]
    ["react" :as react]
    ["react-color" :refer [SketchPicker]]
    [applied-science.js-interop :as j]
    [clojure.string :as str]
    [goog.functions :as functions]
    [immersa.common.firebase :as firebase]
    [immersa.scene.core :as scene.core]
    [immersa.ui.crisp-chat :as crisp-chat]
    [immersa.ui.editor.components.alert-dialog :refer [alert-dialog]]
    [immersa.ui.editor.components.button :refer [button]]
    [immersa.ui.editor.components.canvas-context-menu :refer [canvas-context-menu]]
    [immersa.ui.editor.components.dropdown :refer [dropdown dropdown-item dropdown-separator dropdown-context-menu]]
    [immersa.ui.editor.components.progress :refer [progress]]
    [immersa.ui.editor.components.text :refer [text]]
    [immersa.ui.editor.components.tooltip :refer [tooltip]]
    [immersa.ui.editor.events :as events]
    [immersa.ui.editor.options-panel.views :refer [options-panel]]
    [immersa.ui.editor.slide-panel.views :refer [slides-panel]]
    [immersa.ui.editor.styles :as styles]
    [immersa.ui.editor.subs :as subs]
    [immersa.ui.icons :as icon]
    [immersa.ui.loading-screen :refer [loading-screen]]
    [immersa.ui.subs :as main.subs]
    [immersa.ui.theme.colors :as colors]
    [re-frame.core :refer [dispatch subscribe]]
    [reagent.core :as r])
  (:require-macros
    [immersa.common.macros :as m]))

(defn- canvas [state]
  (r/create-class
    {:component-did-mount #(scene.core/start-scene (js/document.getElementById "renderCanvas")
                                                   {:mode :editor
                                                    :slides @(subscribe [::subs/slides-all])
                                                    :thumbnails @(subscribe [::subs/slides-thumbnails])})
     :reagent-render (fn []
                       [:canvas
                        {:id "renderCanvas"
                         :on-blur #(do
                                     (reset! state :blur)
                                     (dispatch [::events/update-thumbnail]))
                         :on-focus #(reset! state :focus)
                         :class (styles/canvas)}])}))

(defn- canvas-container []
  (let [state (r/atom :blur)]
    (fn []
      (let [{:keys [width height]} @(subscribe [::subs/calculated-canvas-wrapper-dimensions])
            camera-unlocked? (not @(subscribe [::subs/camera-locked?]))]
        (when (and width height (> width 0) (> height 0))
          [:div
           {:id "canvas-container"
            :style {:width (str width "px")
                    :height (str height "px")}
            :class (styles/canvas-container @state camera-unlocked?)}
           [canvas state]
           (when @(subscribe [::main.subs/loading-screen?])
             [loading-screen height])])))))

(defn- canvas-wrapper []
  (let [ref (r/atom nil)
        ob (atom nil)
        on-resize (functions/debounce
                    (fn [entries]
                      (doseq [e entries]
                        (dispatch [::events/set-canvas-wrapper-dimensions
                                   (j/get-in e [:contentRect :width])
                                   (j/get-in e [:contentRect :height])]))
                      (js/setTimeout #(dispatch [::events/resize-scene]) 200))
                    200)]
    (r/create-class
      {:component-did-mount (fn []
                              (println "canvas-wrapper did mount")
                              (when @ref
                                (let [observer (js/ResizeObserver. on-resize)]
                                  (reset! ob observer)
                                  (j/call observer :observe @ref))))
       :component-will-unmount #(some-> @ob (j/call :disconnect))
       :reagent-render (fn []
                         [:div
                          {:id "canvas-wrapper"
                           :ref #(reset! ref %)
                           :class (styles/canvas-wrapper)}
                          (when @ref
                            [canvas-container])])})))

(defn- header-left-panel []
  [:div (styles/title-bar)
   [:div (styles/menubar-list-icon)
    [icon/list-menu {:size 24
                     :color colors/text-primary}]]
   [:div (styles/title-bar-full-width)
    [:div (styles/title-container)
     [:span (styles/title-label) "My 3D Presentation"]
     [:div (styles/private-badge)
      [icon/lock {:size 12}]
      [tooltip
       {:trigger [text {:size :s
                        :weight :light} "Beta"]
        :content "Immersa is currently in beta"}]]]]])

(defn presentation-component [{:keys [icon class disabled? color text-weight on-click]
                               :or {disabled? false}
                               :as opts}]
  [:div {:class [(styles/presentation-component disabled?) class]
         :on-click on-click
         :disabled disabled?}
   [icon {:size 24
          :color (or color colors/text-primary)}]
   [text {:size :s
          :color (or color colors/text-primary)
          :weight (or text-weight :light)} (:text opts)]])

(defn- invisible-img-file-input []
  (let [open? (r/atom false)
        err? (r/atom false)
        limit-exceeded? (r/atom false)
        progress* (r/atom 0)
        task (atom nil)
        max-size 5]
    (fn []
      [:<>
       [alert-dialog
        {:open? @open?
         :title "Uploading image"
         :description (cond
                        @limit-exceeded? (str "The file size exceeds the limit of " max-size "MB. "
                                              "Please choose a smaller file.")
                        @err? "An error occurred while uploading your image. Please try again."
                        :else "Please wait while we upload your image.")
         :content (when-not @limit-exceeded?
                    [progress {:value @progress*
                               :style {:width "100%"}}])
         :cancel-button [button {:text "Cancel"
                                 :on-click (fn []
                                             (when-let [task* @task]
                                               (j/call task* :cancel))
                                             (reset! open? false))}]}]
       [:input
        {:id "file-input"
         :type "file"
         :style {:display "none"}
         :accept ".jpg, .jpeg, .png"
         :on-change (fn [this]
                      (when (-> this .-target .-value (not= ""))
                        (let [^js/File file (-> this .-target .-files (aget 0))]
                          (reset! open? true)
                          (reset! err? false)
                          (reset! limit-exceeded? false)
                          (reset! progress* 0)
                          (reset! task nil)
                          (if (< (j/get file :size) (* 1024 1024 max-size))
                            (firebase/upload-file
                              {:user-id @(subscribe [::main.subs/user-id])
                               :file file
                               :type :image
                               :task-state task
                               :on-progress (fn [percentage]
                                              (reset! progress* percentage))
                               :on-error (fn [_]
                                           (reset! err? true))
                               :on-complete (fn [url]
                                              (reset! open? false)
                                              (dispatch [::events/add-image url])
                                              (dispatch [::events/add-uploaded-image {:name (j/get file :name)
                                                                                      :url url}]))})
                            (reset! limit-exceeded? true))
                          (set! (-> this .-target .-value) ""))))}]])))

(defn- image-component []
  (let [images @(subscribe [::subs/uploaded-images])]
    [dropdown
     {:style (when (<= (count images) 5)
               {:height (str (* 30 (inc (count images))) "px")})
      :trigger [presentation-component {:icon icon/image
                                        :text "Image"}]
      :children [:<>
                 [dropdown-item
                  {:item [:div {:style {:display "flex"
                                        :align-items "center"
                                        :justify-content "space-between"
                                        :width "100%"}}
                          [text {:size :l} "Upload image"]
                          [icon/upload {:size 16}]]
                   :on-select #(some-> (js/document.getElementById "file-input") .click)}]
                 [dropdown-separator]
                 (for [{:keys [name url]} images
                       :let [tr ^{:key url}
                             [dropdown-item
                              {:item [text {:size :l
                                            :weight :light
                                            :style {:white-space "nowrap"
                                                    :overflow "hidden"
                                                    :text-overflow "ellipsis"
                                                    :width "175px"}} name]
                               :on-select #(dispatch [::events/add-image url])}]]]
                   (if (> (count name) 21)
                     ^{:key url}
                     [tooltip
                      {:content name
                       :trigger tr}]
                     tr))]}]))

(defn- header-center-panel []
  [:div (styles/header-center-panel)

   [tooltip
    {:trigger [presentation-component {:icon icon/text
                                       :text "Text"
                                       :on-click #(dispatch [::events/add-text-mesh])}]
     :content "Add text"
     :shortcuts "T"}]
   [image-component]
   [invisible-img-file-input]
   [presentation-component {:icon icon/cube
                            :text "3D Model"
                            :class (styles/presentation-component-cube)
                            :disabled? true}]
   #_[presentation-component {:icon icon/camera
                              :text "Camera"
                              :disabled? true}]
   [presentation-component {:icon icon/student
                            :text "Tutorial"
                            :disabled? true}]
   [presentation-component {:icon icon/chats-circle
                            :text "Feedback"
                            :text-weight :regular
                            :class (styles/presentation-component-cube)
                            :color colors/button-outline-text
                            :on-click #(dispatch [::events/open-crisp-chat])}]
   #_[presentation-component {:icon icon/books
                              :text "Library"
                              :disabled? true}]
   #_[presentation-component {:icon icon/light
                              :text "Light"
                              :disabled? true}]])

(defn- header-right-panel []
  [:div (styles/header-right)
   [:div (styles/header-right-container)
    [button {:text "Present"
             :type :outline
             :class (styles/present-share-width)
             :icon-left [icon/play {:size 18
                                    :weight "fill"
                                    :color colors/button-outline-text}]}]
    [button {:text "Share"
             :type :regular
             :class (styles/present-share-width)
             :icon-right [icon/share {:size 18
                                      :weight "fill"
                                      :color colors/button-text}]}]]])

(defn- header []
  [:div (styles/header-container)
   [header-left-panel]
   [header-center-panel]
   [header-right-panel]])

(defn editor-panel []
  (let [{:keys [user]} (j/lookup (useUser))
        {:keys [getToken]} (j/lookup (useAuth))
        _ (react/useEffect
            (fn []
              (let [user-id (j/get user :id)
                    email (j/get-in user [:primaryEmailAddress :emailAddress])
                    full-name (j/get user :fullName)
                    _ (firebase/init-app)
                    auth (getAuth)]
                (m/js-await [token (getToken #js {:template "integration_firebase"})]
                  (m/js-await [userCredentials (signInWithCustomToken auth token)]))
                (crisp-chat/set-user-email email)
                (when-not (str/blank? full-name)
                  (crisp-chat/set-user-name full-name))
                (firebase/get-last-uploaded-images
                  {:user-id user-id
                   :on-complete #(dispatch [::events/add-uploaded-image %])})
                (dispatch [::events/init-user
                           {:id user-id
                            :full-name full-name
                            :email email
                            :object user}])))
            #js[])]
    [:div (styles/editor-container)
     [header]
     [:div (styles/content-container)
      [slides-panel]
      [canvas-wrapper]
      [options-panel]
      [canvas-context-menu]]]))

(comment
  @(subscribe [::subs/editor])
  @(subscribe [::main.subs/user])
  )
